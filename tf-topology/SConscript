# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('BuildEnv')
env = BuildEnv.Clone()

sources = [
    'MANIFEST.in',
    'CHANGES.txt',
    'LICENSE.txt',
    'README.txt',
    'tf-topology.conf',
    'requirements.txt',
    'setup.py',
    'tf_topology/__init__.py',
    'tf_topology/main.py',
    'tf_topology/analytic_client.py',
    'tf_topology/controller.py',
    'tf_topology/config.py',
    'tf_topology/topology_uve.py',
    'tf_topology/topology_config_db.py',
    'tf_topology/topology_config_handler.py'
    ]

common_sources = [
    '#src/contrail-analytics/contrail-opserver/analytics_logger.py',
    '#src/contrail-analytics/contrail-opserver/config_handler.py'
]

src_sandesh = env.SandeshGenPy('link.sandesh', 'tf_topology/sandesh/', False)
topology_info_pkg = env.SandeshGenPy('topology_info.sandesh', 'tf_topology/sandesh/', False)
cpuinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/cpuinfo.sandesh', 'tf_topology/sandesh/nodeinfo/', False)
process_info_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/process_info.sandesh', 'tf_topology/sandesh/nodeinfo/', False)
nodeinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/nodeinfo.sandesh', 'tf_topology/sandesh/', False)
derived_stats_pkg = env.SandeshGenPy('#src/contrail-common/sandesh/library/common/derived_stats_results.sandesh', 'tf_topology/sandesh/analytics/', False)

common_sources_rules = [env.Install('tf_topology', f) for f in common_sources]

version = '0.1.dev0'               # Make this match what is in setup.py

# XXX Was this disabled/removed as part of repo split? If so, it might
# explain the version discrepancy. Probably this should be done via
# version setting in setup.py
#with open("CHANGES.txt") as f:
#    version = f.read().split(",")[0][1:]

sdist_depends = [ nodeinfo_pkg, cpuinfo_pkg, process_info_pkg, src_sandesh,
                  derived_stats_pkg, topology_info_pkg, common_sources_rules ]
cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_gen = env.Command('dist/tf_topology-' \
        + version + '.tar.gz',
        sources + sdist_depends, cd_cmd + 'python3 setup.py sdist')
env.Depends(sdist_gen, sdist_depends)
env.Alias('src/contrail-analytics/tf-topology/' + \
        'tf_topology:sdist', sdist_gen)

src_rules = [env.Install(Dir("."),
    '#src/contrail-analytics/tf-topology/' + f) for f in sources]
src_rules += [env.Install(Dir("."), f) for f in common_sources]

import os

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

env.Append(ENV = {'BUILDTOP': Dir(env['TOP']).abspath})

doc_files = []
doc_files += env['ANALYTICS_DOC_FILES']
doc_files += env.SandeshGenDoc('link.sandesh')
doc_files += env.SandeshGenDoc('topology_info.sandesh')

if 'install' in BUILD_TARGETS:
    install_whl_cmd = env.Command(
        None, sources + sdist_depends,
        cd_cmd + 'python3 setup.py bdist_wheel --dist-dir /pip')
    env.Depends(install_whl_cmd, sdist_depends)
    env.Alias('install', install_whl_cmd)

env.Alias('install', env.Install(env['INSTALL_CONF'],
            'tf-topology.conf'))
env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/tf-topology/', doc_files))

# Local Variables:
# mode: python
# End:
