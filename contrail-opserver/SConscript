# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import os

Import('BuildEnv')
OpEnv = BuildEnv.Clone()

OpEnv.SConscript('docs/SConscript', exports='OpEnv', duplicate = 0)

base_path = '#src/contrail-analytics/contrail-opserver/'

viz_pkg = OpEnv.SandeshGenPy('#src/contrail-analytics/contrail-collector/viz.sandesh', base_path + 'opserver/sandesh/', False)
analytics_pkg = OpEnv.SandeshGenPy('#src/contrail-analytics/contrail-collector/analytics.sandesh', base_path + 'opserver/sandesh/', False)
cpuinfo_pkg2 = OpEnv.SandeshGenPy('#src/contrail-common/base/sandesh/cpuinfo.sandesh', base_path + 'opserver/sandesh/analytics/', False)
process_info_pkg2 = OpEnv.SandeshGenPy('#src/contrail-common/base/sandesh/process_info.sandesh', base_path + 'opserver/sandesh/analytics/', False)
cpuinfo_pkg = OpEnv.SandeshGenPy('#src/contrail-common/base/sandesh/cpuinfo.sandesh', base_path + 'opserver/sandesh/nodeinfo/', False)
process_info_pkg = OpEnv.SandeshGenPy('#src/contrail-common/base/sandesh/process_info.sandesh', base_path + 'opserver/sandesh/nodeinfo/', False)
nodeinfo_pkg = OpEnv.SandeshGenPy('#src/contrail-common/base/sandesh/nodeinfo.sandesh', base_path + 'opserver/sandesh/', False)
redis_pkg = OpEnv.SandeshGenPy('#src/contrail-analytics/contrail-collector/redis.sandesh', base_path + 'opserver/sandesh/', False)
alarmgen_pkg = OpEnv.SandeshGenPy('alarmgen_ctrl.sandesh', base_path + 'opserver/sandesh/', False)
alarm_sandesh_base_pkg = OpEnv.SandeshGenPy('#src/contrail-common/sandesh/library/common/sandesh_alarm_base.sandesh', base_path + 'opserver/sandesh/alarmgen_ctrl/', False)
derived_stats_pkg = OpEnv.SandeshGenPy('#src/contrail-common/sandesh/library/common/derived_stats_results.sandesh', base_path + 'opserver/sandesh/analytics/', False)
database_pkg = OpEnv.SandeshGenPy('#src/contrail-common/database/gendb.sandesh', base_path + 'opserver/sandesh/viz/', False)
analytics_api_info_pkg = OpEnv.SandeshGenPy('analytics_api_info.sandesh', base_path + 'opserver/sandesh/', False)
OpEnv.Depends(alarm_sandesh_base_pkg, alarmgen_pkg)
OpEnv.Depends(derived_stats_pkg, analytics_pkg)

vm_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-opserver/test/sandesh/virtual_machine.sandesh',
        base_path + 'test/sandesh/', False)
vn_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-opserver/test/sandesh/virtual_network.sandesh',
        base_path + 'test/sandesh/', False)
stats_test_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-opserver/test/sandesh/stats_test.sandesh',
        base_path + 'test/sandesh/', False)
alarm_test_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-opserver/test/sandesh/alarm_test.sandesh',
        base_path + 'test/sandesh/', False)
object_table_test_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-opserver/test/sandesh/object_table_test.sandesh',
        base_path + 'test/sandesh/', False)
flow_pkg = OpEnv.SandeshGenPy(
        '#controller/src/sandesh/common/flow.sandesh',
        base_path + 'test/sandesh/', False)
qe_test_pkg = OpEnv.SandeshGenPy(
        '#src/contrail-analytics/contrail-query-engine/qe.sandesh',
        base_path + 'test/sandesh/', False)
sandesh_test_pkgs = [
    vm_pkg, vn_pkg, stats_test_pkg, alarm_test_pkg,
    object_table_test_pkg, flow_pkg, qe_test_pkg
]

sdist_depends = [
    viz_pkg, analytics_pkg, cpuinfo_pkg, redis_pkg,
    process_info_pkg,
    alarmgen_pkg, database_pkg, alarm_sandesh_base_pkg,
    derived_stats_pkg, nodeinfo_pkg, cpuinfo_pkg2,
    process_info_pkg2, analytics_api_info_pkg,
]

if 'install' in BUILD_TARGETS:
    # Documentation
    opserver_doc_files = []
    opserver_doc_files += OpEnv['ANALYTICS_DOC_FILES']
    opserver_doc_files += OpEnv.SandeshGenDoc('analytics_api_info.sandesh')
    OpEnv.Alias('install', OpEnv.Install(
        OpEnv['INSTALL_MESSAGE_DOC'] + '/contrail-analytics-api/',
        opserver_doc_files))

    alarmgen_doc_files = []
    alarmgen_doc_files += OpEnv['ANALYTICS_DOC_FILES']
    alarmgen_doc_files += OpEnv.SandeshGenDoc('alarmgen_ctrl.sandesh')
    alarmgen_doc_files += OpEnv.SandeshGenDoc('#src/contrail-common/sandesh/library/common/sandesh_alarm_base.sandesh', OpEnv['TOP'] + '/opserver/')
    OpEnv.Alias('install', OpEnv.Install(
        OpEnv['INSTALL_MESSAGE_DOC'] + '/contrail-alarm-gen/',
        alarmgen_doc_files))

sdist_gen = OpEnv.Command(
    '/pip/opserver-0.1.dev0-py3-none-any.whl', 'setup.py',
    'cd ' + Dir(base_path).path + ' && ' + 'python3 setup.py bdist_wheel --dist-dir /pip')
OpEnv.Depends(sdist_gen, sdist_depends)
OpEnv.Alias('install', sdist_gen)
OpEnv.Alias("contrail-analytics-api", sdist_gen)

deps = [
    '/pip/sandesh-0.1.dev0-py3-none-any.whl',
    '/pip/sandesh_common-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_api_client-0.1.dev0-py3-none-any.whl',
    '/pip/cfgm_common-0.1.dev0-py3-none-any.whl',
    '/pip/libpartition-0.1.dev0-py3-none-any.whl',
    '/pip/contrail_config_utils-0.1.dev0-py3-none-any.whl',
    'contrail-collector',
    'src/query_engine:qedt',
]

test_depends = []
if 'src/contrail-analytics/contrail-opserver:test' in BUILD_TARGETS:
    test_depends.append(OpEnv.Install(Dir('.'), '#src/contrail-analytics/contrail-opserver/test'))

OpEnv.SetupPyTestSuiteWithDeps(sdist_gen + sandesh_test_pkgs + test_depends, sdist_depends=deps, top_dir=Dir(base_path).abspath)
